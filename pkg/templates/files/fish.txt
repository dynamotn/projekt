# vim:filetype=fish
function pj --description "Jump to a project"
  set -l argc (count $argv)
  if test $argc -le 0 -o $argc -gt 1
    return 1
  end
  set -l target (projekt folder get --verbose=error $argv[1])
  if test -d "$target"
    cd $target
    test -n "$ZELLIJ"; and zellij action rename-tab $argv[1]
    test -n "$TMUX"; and tmux rename-window $argv[1]
  else
    echo "No such project: $argv[1]"
    return 1
  end
end

function __pj_needs_command
  set cmd (commandline -opc)
  if [ (count $cmd) -eq 1 ]
    return 0
  else
    # Skip first word because it's "pj" or a wrapper
    for c in $cmd[2..-1]
      switch $c
        # We assume that any other token that's not an argument to a general option is a command
        case "*"
          return 1
      end
    end
    return 0
  end
  return 1
end

function __pj_completion
  command projekt folder list --no-headers --short-only --verbose=error | while read -l project
    set -l name (basename $project)
    complete -f -c pj -n '__pj_needs_command' -a $name -d "Change to $project folder"
  end
end

function projekt
  command projekt $argv
  __pj_completion
end

__pj_completion
